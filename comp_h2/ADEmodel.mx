#NGroups 4
#define nvar 1
#define twonvar 2
Title SSS
Data calculation
 Begin Matrices;
  W Lower nvar nvar Free     !nonadditive/dominance genetic effects
  X Lower nvar nvar Free     ! additive genetic structure
  Z Lower nvar nvar Free     ! specific environmental structure
  H Full 1 1
  Q Full 1 1
End Matrices;
 Matrix H .5    ! weight of additive genetic effect in DZ twins
Matrix Q .25
Begin Algebra;
  A= X*X'; ! additive genetic covariance matrix
  D= W*W'; ! dominance genetic covariance matrix
  E= Z*Z'; ! non shared environmental covariance matrix
End Algebra;
End
G2: Monozygotic twin pairs
 Data NI=twonvar NO=NUM_PAIRS_MZ  ! number of pairs
 Labels T1_VAR T2_VAR
 CMatrix
  MZtwin1var MZcovar MZtwin2var ! var_t1 covar var_t2
  Matrices= Group 1
 Covariances A+D+E | A+D _
             A+D   | A+D+E ;
 Options RSiduals
End
G3: Dizygotic twin pairs
 Data NI=twonvar NO=NUM_PAIRS_DZ  ! number of pairs
 Labels T1_VAR T2_VAR
 CMatrix
 DZtwin1var DZcovar DZtwin2var ! var_t1 covar var_t2
Matrices= Group 1
 Covariances H@A+Q@D+E | H@A+Q@D _
             H@A+Q@D   | H@A+Q@D+E ;
 Start .6 All
 Options Multiple RSiduals
End

G4: Calculation of standardized solution
Calculation
Matrices= Group 1
I Iden nvar nvar
Begin Algebra;
P= A+D+E;
V = \sqrt(\v2d(\d2v(P)));
Q = V~ * A * V~;   ! additive genetic
T = V~ * D * V~;   ! non-additive genetic
S = V~ * E * V~;   ! nonshared environmental
G= \sqrt(I.P)~*X; ! standardized parameter estimates
K= \sqrt(I.P)~*W;
L= \sqrt(I.P)~*Z;
End Algebra;
Option RSiduals
End



